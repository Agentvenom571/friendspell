apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

tasks.matching {it instanceof Test}.all {
  testLogging.events = ["failed", "passed", "skipped"]
}

android {
  compileSdkVersion 24
  buildToolsVersion '24.0.1'

  defaultConfig {
    applicationId 'com.sqisland.friendspell'
    minSdkVersion 15
    targetSdkVersion 24
    versionCode 3
    versionName '1.0.2'

    testInstrumentationRunner 'com.sqisland.friendspell.util.MockTestRunner'
  }

  buildTypes {
    debug {
      testCoverageEnabled true
    }
  }

  productFlavors {
    canned {
    }
    normal {
    }
  }

  // Remove cannedRelease as it's not needed.
  android.variantFilter { variant ->
    if(variant.buildType.name.equals('release')
            && variant.getFlavors().get(0).name.equals('canned')) {
      variant.setIgnore(true);
    }
  }

  packagingOptions {
    exclude 'LICENSE.txt'
  }
}

final DAGGER_VERSION = '2.5'
final DEXMAKER_VERSION = '1.2'
final BUTTERKNIFE_VERSION = '8.2.1'
final MOCKITO_VERSION = '1.10.19'
final GOOGLE_PLAY_SERVICES_VERSION = '9.2.1'
final SUPPORT_LIB_VERSION = '24.1.1'
final ESPRESSO_VERSION = '2.2.2'

dependencies {
  compile "com.google.dagger:dagger:${DAGGER_VERSION}"
  apt "com.google.dagger:dagger-compiler:${DAGGER_VERSION}"
  provided 'javax.annotation:javax.annotation-api:1.2'

  compile "com.android.support:appcompat-v7:${SUPPORT_LIB_VERSION}"
  compile "com.google.android.gms:play-services-plus:${GOOGLE_PLAY_SERVICES_VERSION}"
  compile "com.google.android.gms:play-services-nearby:${GOOGLE_PLAY_SERVICES_VERSION}"
  compile "com.google.android.gms:play-services-auth:${GOOGLE_PLAY_SERVICES_VERSION}"

  compile 'nl.qbusict:cupboard:2.1.4'
  compile 'com.google.code.gson:gson:2.7'
  compile 'com.squareup.picasso:picasso:2.5.2'

  compile 'com.jakewharton.timber:timber:4.1.2'
  compile "com.jakewharton:butterknife:${BUTTERKNIFE_VERSION}"
  apt "com.jakewharton:butterknife-compiler:${BUTTERKNIFE_VERSION}"

  testCompile 'junit:junit:4.12'

  androidTestCompile ('com.android.support.test:runner:0.5') {
    exclude module: 'support-annotations'
  }
  androidTestCompile ('com.android.support.test:rules:0.5') {
    exclude module: 'support-annotations'
  }
  androidTestCompile ("com.android.support.test.espresso:espresso-core:${ESPRESSO_VERSION}") {
    exclude module: 'support-annotations'
  }
  androidTestCompile ("com.android.support.test.espresso:espresso-intents:${ESPRESSO_VERSION}") {
    exclude module: 'support-annotations'
  }

  androidTestCompile "com.google.dexmaker:dexmaker:${DEXMAKER_VERSION}"
  androidTestCompile "com.google.dexmaker:dexmaker-mockito:${DEXMAKER_VERSION}"
  androidTestCompile "org.mockito:mockito-core:${MOCKITO_VERSION}"
  androidTestApt "com.google.dagger:dagger-compiler:${DAGGER_VERSION}"
}

apply plugin: 'com.google.gms.google-services'